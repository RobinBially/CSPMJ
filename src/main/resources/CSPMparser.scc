/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
 * A Parser for CSP in JAVA                           		  *
 * CSPMJ V0.61												  *
 *															  *
 * robin.bially@hhu.de		                                  *
 * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

Package CSPMparser;
Helpers

	all = [0 .. 0xFFFF];
	dangle_l = 0x00AB;
	dangle_r = 0x00BB;
	char = [all- ['''+'"']];
	digits = ['0' .. '9'];
	first_digit = [digits - '0'];
	highc = ['A' .. 'Z'];
	lowc = ['a' .. 'z'];
	letter = (highc | lowc | '_');
	letter_digits = (letter|digits);	
    tab = 9;
    cr = 13;
    lf = 10;
	not_cr_lf = [all - [cr + lf]];	
	no_brace_r = [all - '}'];
	eol = cr lf | cr | lf;
	blank = (' '|tab);
	white = (' '|tab|cr|lf)*;
	
/*	Triangle bracket substitutions:
	\u00AB = «	seq opening
	\u00BB = »	seq closing
	\u00A3 = £	greater
	\u20AC = €  smaller
*/
	seq_open	=	0x00AB;
	seq_close	=	0x00BB;
	greater 	=	0x00A3;
	less		=	0x20AC;

Tokens

//CSPM command words
	ltl				= 'LTL';
	ctl				= 'CTL';	
	if 				= 'if';
	then 			= white 'then' white;
	else 			= white 'else' white;
	let 			= 'let' white;
	within 			= 'within' white;
	d_type 			= 'datatype';
	s_type 			= 'subtype';
	n_type 			= 'nametype';
	chan 			= 'channel';
	transparent 	= 'transparent';
	external		= 'external';
	assert 			= 'assert'| 'assert not';
	include 		= 'include';
	print 			= 'print';
	dl_free 		= 'deadlock free';
	div_free 		= 'divergence free'|'livelock free';
	det 			= 'deterministic';
	has_trace 		= 'has trace';
	timed 			= 'Timed';
	mod_defs		= 'module';	
	exports			= 'exports';
	endmodule		= 'endmodule';
	and 			= white 'and' white;
	or 				= white 'or' white;
	not 			= white 'not' white;	
	true			= 'true';
	false			= 'false';
//---------------------------------------------------------------------------------------------------------------------------------------------------
	comma 			= ',' white;
	apo 			= ''';
	quote			= '"';
	dotdot 			= white '..' white;
	ddot 			= white ':' white;
	eq 				= '=' white;
	at 				= '@' white;
	hash 			= '#' white;
	b_slash 		= white '\' white;
	q_mark 			= '?';
	excl_mark 		= '!';
	dollar 			= '$';
	dpipe 			= white '||' white;
	pipe_equals		= white '|=' white; 
	pipe 			= white '|' white;
	doubleat 		= white '@@' white;
	i_leaving 		= white '|||' white;
	i_choice  		= white '|~|' white;
	e_choice  		= white '[]' white;
	interrupt 		= white '/\' white;
	timeout			= white '[>' white;
//---------------------------------------------------------------------------------------------------------------------------------------------------	
	par_l 			= '(' white;
	par_r 			=  white ')';
	empty_map 		= '(| |)';
	lge 			= white '<=' white | white '>=' white;
	seq_open		= seq_open white;
	seq_close 		= white seq_close;
	greater 		= white greater;
	less	 		= less white;		
	tria_l			= '<' white;
	tria_r			= white '>';
	sync_par_l		= white '[+' white;	
	sync_par_r		= white '+]' white;
	sync_int_l 		= white '/+' white;
	sync_int_r		= white '+\' white;
	dbracket_l 		= white '[[' white;
	dbracket_r 		= white ']]';
	dp_bracket_l 	= white ':[' white;
	dp_bracket_r 	= white ']:' white;
	bracket_pipe_l	= white '[|' white;
	bracket_pipe_r	= white '|]' white;
	bracket_l 		= white '[' white;
	bracket_r 		= white ']';
	brace_l 		= '{' white;
	brace_r 		= white '}';
//---------------------------------------------------------------------------------------------------------------------------------------------------	
	guard 			= white '&' white;
	dot 			= white '.' white;
	semicolon 		= white ';' white;
	prefix			= white '->' white;
	arrow_l 		= '<-';
	cat 			= white '^' white;
	d_arr 			= '<->';	
	equi 			= '<=>';
	imp_r 			= '=>';
	imp_l 			= '<=';
	equal			= white '==' white | white '!=' white;	
	tra_mod 		= white '[T=' white;
	fail_mod  		= white '[F=' white;
	fail_div 		= white '[FD=' white;
	refusal			= white '[R=' white;	
	model 			= white ('[T]'|'[F]'|'[FD]') white;	
	mul_div_mod 	= white '*' white | white '/' white | white '%' white;
	plus 			= white '+' white;
	minus		    = '-';
	wildcard 		= '_';
//---------------------------------------------------------------------------------------------------------------------------------------------------	
	/*Strings*/ 
	number 			= '0' | first_digit digits*;
	identifier 		= letter letter_digits* '''*;
	nl				= white eol white;
	string			= '"' char* '"';
	char			= ''' char ''';
	blank			= blank*;
	
Ignored Tokens
	blank;
	
Productions

start {-> start}	=	nl* defs? 											{-> New start.defs([defs.def])};

defs {-> def*} 		= 	 {sinlge} [decl]:def nl*							{-> [decl.def]}
						|{multiple} [first]:def nl [rest]:defs 				{-> [first.def,rest.def]}
						;

def					=	 {expression} exp									{-> New def.expression(exp)}
						|{types} types										{-> New def.types(types)}
						|{channel} channel									{-> channel.def}
						|{assertion} assertion								{-> New def.assertion(assertion)}
						|{print} print proc1 								{-> New def.print_exp(proc1.exp)}
						|{timed_section} time								{-> time.def}
						|{module} module									{-> module.def}
						|{include_file} include_file						{-> include_file.def}
						|{trans} transparent id_list						{-> New def.transparent([id_list.id])}
						|{ext} external id									{-> New def.external(id)}
						; 
//---------------------------------------------------------------------------------------------------------------------------------------------------
time {->def} =  timed par_l proc1 par_r brace_l defs brace_r 				{-> New def.timed(proc1.exp,[defs.def])};
//---------------------------------------------------------------------------------------------------------------------------------------------------
module {->def} = mod_defs id [ldefs]:defs exports [rdefs]:defs endmodule 	{-> New def.module(id,[ldefs.def],[rdefs.def])};
//---------------------------------------------------------------------------------------------------------------------------------------------------
/*Include File if path is valid*/
include_file {->def} = include string 										{-> New def.include_file(string)};		
//---------------------------------------------------------------------------------------------------------------------------------------------------
id 					= 	identifier 					{-> New id.identifier(identifier)};
id_list {->id+}		= 	{list} id comma id_list		{-> [id,id_list.id]}
						|{end} id					{-> [id]}
						;
//---------------------------------------------------------------------------------------------------------------------------------------------------
types					= 	{dtype} d_type typedef 					{-> New types.dtype(typedef.types)}
						   |{stype} s_type typedef 					{-> New types.stype(typedef.types)}
						   |{ntype} ntype							{-> ntype.types}
						   ;
typedef {->types}		=  id eq typedef_list						{-> New types.typedef(id,[typedef_list.types])};
clause {->types}		=  [clause_name]:id dotted?					{-> New types.clause(clause_name,dotted.types)};
dotted {->types}  		=  dot type_exp								{-> New types.dotted(type_exp)};
typedef_list {->types+} =  {single} clause pipe typedef_list		{-> [clause.types,typedef_list.types]}
						  |{end} clause								{-> [clause.types]}
						  ;
ntype {->types}			=  n_type id eq type_exp					{-> New types.ntype(id,type_exp)};	  
//---------------------------------------------------------------------------------------------------------------------------------------------------
/*Creation of Channels*/
channel {-> def}			=  {channel} chan chan_list chan_type?			{-> New def.channel([chan_list.id],chan_type.type_exp)};
chan_list {->id+} 			=  {list} [channel_name]:id comma chan_list 	{-> [channel_name.id,chan_list.id]}
							  |{end} [channel_name]:id						{-> [channel_name.id]}
							  ;
chan_type {-> type_exp}		= ddot type_exp									{-> type_exp.type_exp};
//---------------------------------------------------------------------------------------------------------------------------------------------------			
type_exp 					=    type_exp0								{-> New type_exp.dotted([type_exp0.type_exp])};

type_exp0 {->type_exp+}		=  	 {dotted} type_exp1 dot type_exp0		{-> [type_exp1.type_exp,type_exp0.type_exp]}
								|{par} type_exp1						{-> [type_exp1.type_exp]}
								;
type_exp1 {->type_exp}		= 	 {par} par_l type_exp_list par_r		{-> New type_exp.par([type_exp_list.type_exp])}
								|{set_type} type_exp2					{-> type_exp2.type_exp}
								;
type_exp2 {->type_exp} 		=  	 {set} set								{-> New type_exp.set(set.exp)}
								|{set_name} id lambda?					{-> New type_exp.id(id,lambda)}
								;
type_exp_list {->type_exp+}  = 	 {list} type_exp0 comma type_exp_list	{->[type_exp0.type_exp,type_exp_list.type_exp]}
								|{end} type_exp0						{->[type_exp0.type_exp]}
								;
			
//---------------------------------------------------------------------------------------------------------------------------------------------------
parameters 	  =	{parameters} par_l pattern_list par_r 						{-> New parameters.parameters([pattern_list.pattern])};
//---------------------------------------------------------------------------------------------------------------------------------------------------
							
pattern1 {->pattern}   	  =	{double_pattern} double_list					{-> New pattern.double([double_list.pattern])};
double_list {->pattern+}  = {list} double_list doubleat pattern2			{->[double_list.pattern,pattern2.pattern]}
							|{end} pattern2									{->[pattern2.pattern]}
							;
pattern2 {->pattern}	  =  {dot_pattern} dot_list							{-> New pattern.dot([dot_list.pattern])};							
dot_list {->pattern+}	  =  {list} dot_list dot pattern3 					{-> [dot_list.pattern,pattern3.pattern]}
							|{end2} pattern3								{-> [pattern3.pattern]}
							;
pattern3 {->pattern}	  =  {concat_pattern} concat_list					{-> New pattern.concat([concat_list.pattern])};
concat_list {->pattern+}  =  {list} concat_list cat pattern_atom			{-> [concat_list.pattern, pattern_atom.pattern]}
							|{end} pattern_atom								{-> [pattern_atom.pattern]}
							;
pattern_atom {->pattern}  =  {set} set_pattern 						{-> set_pattern.pattern}
							|{list} list_pattern 					{-> list_pattern.pattern}
							|{par} par_pattern 						{-> par_pattern.pattern}
							|{tuple} tuple_pattern 					{-> tuple_pattern.pattern}
							|{wildcard} wildcard 					{-> New pattern.wildcard(wildcard)}
							|{var} id 								{-> New pattern.var(id)}
							|{string} string						{-> New pattern.string(string)}
							|{char} char							{-> New pattern.char(char)}
							|{number} number						{-> New pattern.number(number)}
							|{true} true							{-> New pattern.true(true)}
							|{false} false							{-> New pattern.false(false)}
							;
				
set_pattern	{->pattern}  	=    {empty} brace_l brace_r       				{-> New pattern.empty_set()}
								|{element} brace_l pattern1 brace_r			{-> New pattern.set(pattern1.pattern)}
								;
				
list_pattern  {->pattern}	=    {list} seq_open pattern_list seq_close		{-> New pattern.list([pattern_list.pattern])}
								|{empty_list} seq_open seq_close 			{-> New pattern.empty_list()}
								;
pattern_list {->pattern+}	=  	 {list} pattern1 comma pattern_list			{-> [pattern1.pattern,pattern_list.pattern]}
								|{end} pattern1								{-> [pattern1.pattern]}
								;

par_pattern {->pattern}	  	=  par_l pattern1 par_r							{-> New pattern.par(pattern1.pattern)};

tuple_pattern {->pattern}   =  par_l pattern1 comma pattern_list par_r 		{-> New pattern.tuple(pattern1.pattern,[pattern_list.pattern])};

//---------------------------------------------------------------------------------------------------------------------------------------------------
/*Creation of Expressions*/	

exp  				=	 {function} id parameters eq proc1 								{-> New exp.function(id,parameters,proc1.exp)}
						|{pattern} pattern1 eq proc1 									{-> New exp.pattern(pattern1.pattern,proc1.exp)}
						;

proc1 {->exp}		=  	 {hide} proc1 b_slash proc2 									{-> New exp.hide(proc1.exp,b_slash,proc2.exp)}
						|{p2} proc2 													{-> proc2.exp}
						;
proc2 {->exp}		=    {ileave} proc2 i_leaving proc3 								{-> New exp.ileave(proc2.exp,i_leaving,proc3.exp)}
						|{p3} proc3 													{-> proc3.exp}
						;
proc3 {->exp}		=  	 {except} proc3 bracket_pipe_l event pipe tria_r proc4			{-> New exp.except(proc3.exp,bracket_pipe_l,event.exp,tria_r,proc4.exp)}	
						|{gen_par} proc3 bracket_pipe_l event bracket_pipe_r proc4		{-> New exp.gen_par(proc3.exp,bracket_pipe_l,event.exp,bracket_pipe_r,proc4.exp)}
						|{alph_par} proc3 bracket_l [eventl]:event dpipe
								[eventr]:event bracket_r proc4 							{-> New exp.alph_par(proc3.exp,bracket_l,eventl.exp,eventr.exp,bracket_r,proc4.exp)}
						|{linked_par} proc3 bracket_l link_comp bracket_r proc4			{-> New exp.linked_par(proc3.exp,bracket_l,link_comp,bracket_r,proc4.exp)}
						|{p4} proc4 													{-> proc4.exp}
						;			
proc4 {->exp}		=  	 {int_choice} proc4 i_choice proc5								{-> New exp.int_choice(proc4.exp,i_choice,proc5.exp)} 
						|{p5} proc5														{-> proc5.exp}
						;
proc5 {->exp}		=  	 {ext_choice} proc5 e_choice proc6 								{-> New exp.ext_choice(proc5.exp,e_choice,proc6.exp)} 
						|{sync_ext} proc5 sync_par_l event sync_par_r proc6				{-> New exp.sync_ext(proc5.exp,sync_par_l,event.exp,sync_par_r,proc6.exp)} 
						|{p6} proc6 													{-> proc6.exp}
						;
proc6 {->exp}		= 	 {interrupt} proc6 interrupt proc7								{-> New exp.interrupt(proc6.exp,interrupt,proc7.exp)}									
						|{sync_interrupt} proc6 sync_int_l event sync_int_r proc7		{-> New exp.sync_interrupt(proc6.exp,sync_int_l,event.exp,sync_int_r,proc7.exp)}
						|{p7} proc7 													{-> proc7.exp}
						;
proc7 {->exp}		= 	 {sliding_choice} proc7 timeout proc8 					{-> New exp.sliding_choice(proc7.exp,timeout,proc8.exp)}
						|{p8} proc8 											{-> proc8.exp}
						;
proc8 {->exp}		= 	 {seq_composit} proc8 semicolon proc9					{-> New exp.seq_composition(proc8.exp,semicolon,proc9.exp)}
						|{p9} proc9 											{-> proc9.exp}
						; 
proc9 {->exp}		= 	 {guard} dot_op guard proc9								{-> New exp.guard(dot_op.exp,guard,proc9.exp)}
						|{prefix} event prefix proc9							{-> New exp.prefix(event.exp,prefix,proc9.exp)}
						|{lambda_term} b_slash pattern_list at proc9			{-> New exp.lambda([pattern_list.pattern],proc9.exp)}
						|{let_within} let defs within proc9						{-> New exp.let_within([defs.def],within,proc9.exp)}
						|{if_else} if bool_exp then proc1 else proc9			{-> New exp.if_else(if,bool_exp.exp,then,proc1.exp,else,proc9.exp)}
						|{replicated} rep 										{-> New exp.replicated(rep.exp)}
						|{p10} proc10											{-> proc10.exp}
						;				
proc10 {->exp} 		=	 {renaming} rename_comp 								{-> New exp.renaming(rename_comp)}
						|{e_renaming} proc10 dbracket_l dbracket_r				{-> New exp.e_renaming(proc10.exp,dbracket_l,dbracket_r)}
						|{event} event 											{-> event.exp}
						;
			
event {->exp}			=	{event} dpattern f1_list? f2_list?				{-> New exp.event(dpattern.exp,[f1_list.pattern],[f2_list.pattern])};	
f1_list	{->pattern+}	=	{list} field1 f1_list							{-> [field1.pattern,f1_list.pattern]}
						   |{end} field1									{-> [field1.pattern]}
						   ;
f2_list {->pattern+}	=	 {list} field2 f2_list							{-> [field2.pattern,f2_list.pattern]}
							|{end} field2									{-> [field2.pattern]}
							;
field1 	{->pattern}	= 	 {nondet_input} dollar pattern1	restriction?		{-> New pattern.nondet_input(pattern1.pattern,restriction.exp)};
field2 	{->pattern}	= 	 {input} q_mark pattern1 restriction?				{-> New pattern.input(pattern1.pattern,restriction.exp)}
					    |{output} excl_mark dot_op							{-> New pattern.output(dot_op.exp)}
						;
restriction {->exp} =  {restricted} ddot dot_op								{-> New exp.restriction(dot_op.exp)};

dpattern {->exp}	=    {dpattern} double_list2							{-> New exp.dpattern([double_list2.exp])};
double_list2 {->exp+} =  {list} dot_op doubleat double_list2				{-> [dot_op.exp,double_list2.exp]}
						|{end} dot_op										{-> [dot_op.exp]}
						;
dot_op {->exp}		=	 dot_op_list										{-> New exp.dot_op([dot_op_list.exp])};
dot_op_list {->exp+}	= 	 {list} bool_exp dot dot_op_list					{-> [bool_exp.exp,dot_op_list.exp]}
						|{end} bool_exp										{-> [bool_exp.exp]}
						;
				
bool_exp {->exp} 	= 	 {or} bool_exp or bool_exp2							{-> New exp.or(bool_exp.exp,bool_exp2.exp)} 
						|{bool_exp2} bool_exp2 								{-> bool_exp2.exp}
						;
bool_exp2 {->exp} 	= 	 {and} bool_exp2 and bool_exp3						{-> New exp.and(bool_exp2.exp,bool_exp3.exp)}  
						|{bool_exp3} bool_exp3 								{-> bool_exp3.exp}
						; 		
bool_exp3 {->exp} 	= 	 {not} not bool_exp3 								{-> New exp.not(bool_exp3.exp)}  	
						|{bool_exp4} bool_exp4 								{-> bool_exp4.exp}
						;
bool_exp4 {->exp}	= 	 {equal} bool_exp4 equal val_exp					{-> New exp.equal(bool_exp4.exp,equal,val_exp.exp)} 
						|{ordering_lge} bool_exp4 lge val_exp				{-> New exp.ordering_lge(bool_exp4.exp,lge,val_exp.exp)} 
						|{ordering_less} bool_exp4 less val_exp 			{-> New exp.ordering_less(bool_exp4.exp,val_exp.exp)} 
						|{ordering_greater} bool_exp4 greater val_exp 		{-> New exp.ordering_greater(bool_exp4.exp,val_exp.exp)} 
						|{val_exp} val_exp 									{-> val_exp.exp}
						;				
val_exp {->exp}		= 	 {addition} val_exp plus val_exp1 					{-> New exp.addition(val_exp.exp,val_exp1.exp)}
						|{subtraction} val_exp minus val_exp1				{-> New exp.subtraction(val_exp.exp,val_exp1.exp)}
						|{val_exp1} val_exp1 								{-> val_exp1.exp}
						;		 			  			
val_exp1 {->exp}	= 	 {multiplication} val_exp1 mul_div_mod val_exp2 	{-> New exp.multiplication(val_exp1.exp,mul_div_mod,val_exp2.exp)}
						|{val_exp2} val_exp2 								{-> val_exp2.exp}
						;		
val_exp2 {->exp}	= 	 {un_minus} minus val_exp2						{-> New exp.un_minus(minus, val_exp2.exp)} 
						|{length} length 								{-> length.exp}
						;				
length {->exp}		= 	 {length} hash length 							{-> New exp.length(hash, length.exp)}
						|{concat} atom cat concat 						{-> New exp.concat(atom.exp,cat,[concat.exp])}
						|{atom} atom									{-> atom.exp}
						;
concat 	{->exp+}	=	 {list} concat cat atom							{-> [concat.exp,atom.exp]}
						|{end} atom										{-> [atom.exp]}
						;						
atom {->exp}		= 	 {sequence} sequence 								{-> sequence.exp}
						|{set} set 											{-> set.exp}
						|{map} map 											{-> map.exp}
						|{tuple} tuple lambda? 								{-> New exp.tuple(tuple,lambda)}
						|{parenthesis} par_l proc1 par_r lambda?			{-> New exp.parenthesis(par_l,proc1.exp,par_r,lambda)}
						|{string} string 									{-> New exp.string(string)}
						|{char} char 										{-> New exp.char(char)}
						|{wildcard} wildcard 								{-> New exp.wildcard(wildcard)}
						|{num} number  										{-> New exp.number(number)}
						|{true} true 										{-> New exp.true(true)}
						|{false} false 										{-> New exp.false(false)}
						|{id} id lambda?									{-> New exp.id(id,lambda)}
						;
//---------------------------------------------------------------------------------------------------------------------------------------------------
/*(e1,e2,...,en) different types possible*/
tuple 	 		=  {tuple} par_l proc1 comma arguments_list par_r			{-> New tuple.tuple(par_l,proc1.exp,comma,[arguments_list.exp],par_r)};
lambda 			=  {lambda} par_l arguments_list par_r						{-> New lambda.lambda([arguments_list.exp])};

//---------------------------------------------------------------------------------------------------------------------------------------------------
// e1,e2,...,en    all of type a! The first node seems to be pointless but it is not!

arguments {-> arguments}	=   arguments_list 									{-> New arguments.arg_list([arguments_list.exp])}; 
arguments_list {-> exp+}	=   {arg_start} proc1 comma arguments_list			{-> [proc1.exp, arguments_list.exp]}
								|{arg_end} proc1								{-> [proc1.exp]}
								;
//---------------------------------------------------------------------------------------------------------------------------------------------------
link_comp 			= lc_list add_stmts?											{-> New link_comp.link_comp([lc_list.exp],add_stmts.stmts)};

lc_list {-> exp+}	=  {list} [levent]:event d_arr [revent]:event comma lc_list		{-> [levent.exp,revent.exp,lc_list.exp]}
					  |{end} [levent]:event d_arr [revent]:event					{-> [levent.exp,revent.exp]}
					  ;
		
rename_comp			= proc10 dbracket_l rc_list  add_stmts? dbracket_r 		{-> New rename_comp.rename_comp(proc10.exp,dbracket_l,[rc_list.exp],add_stmts.stmts,dbracket_r)};

rc_list {-> exp+} 	=  {list} [levent]:event arrow_l [revent]:event comma rc_list 	{-> [levent.exp, revent.exp,rc_list.exp]}
					  |{end} [levent]:event arrow_l [revent]:event					{-> [levent.exp, revent.exp]}
					  ;

add_stmts {-> stmts}	= pipe stmts 												{-> stmts.stmts};
//---------------------------------------------------------------------------------------------------------------------------------------------------
sequence {->exp} =	 {empty_seq} seq_open seq_close									{-> New exp.empty_seq(seq_open,seq_close)}										
					|{expl_seq} seq_open arguments seq_close						{-> New exp.expl_seq(seq_open,arguments,seq_close)}
					|{ranged} seq_open [lval]:val_exp dotdot 
							   [rval]:val_exp seq_close								{-> New exp.ranged_seq(seq_open,lval.exp,rval.exp,seq_close)}
					|{infinite} seq_open val_exp dotdot seq_close					{-> New exp.infinite_seq(seq_open,val_exp.exp,seq_close)}
					|{compr} seq_open arguments pipe stmts seq_close 				{-> New exp.compr_seq(seq_open,arguments,stmts,seq_close)}

					|{ranged_compr} seq_open [lval]:val_exp dotdot 
								 [rval]:val_exp pipe stmts seq_close 				{-> New exp.ranged_compr_seq(seq_open,lval.exp,rval.exp,stmts,seq_close)}
			
					|{infinite_compr} seq_open val_exp dotdot pipe stmts seq_close	{-> New exp.infinite_compr_seq(seq_open,val_exp.exp,stmts,seq_close)}
					;
				 
//---------------------------------------------------------------------------------------------------------------------------------------------------				 
/*Examples: {v,w,x,y,z}, {m..n}, {m..},{x*2..64|x<-{8,4}}*/

set {-> exp} = 	 {empty_set} brace_l brace_r 											{-> New exp.empty_set(brace_l,brace_r)}
				|{set} brace_l arguments brace_r										{-> New exp.set(brace_l,arguments,brace_r)}
				|{ranged} brace_l [lval]:val_exp dotdot 							
										[rval]:val_exp brace_r							{-> New exp.ranged_set(brace_l,lval.exp,rval.exp,brace_r)}
				|{infinite} brace_l val_exp dotdot brace_r								{-> New exp.infinite_set(brace_l,val_exp.exp,brace_r)}
				|{compr} brace_l arguments pipe stmts brace_r							{-> New exp.compr_set(brace_l,arguments,stmts,brace_r)}
				|{ranged_compr} brace_l [lval]:val_exp dotdot 
									   [rval]:val_exp pipe stmts brace_r				{-> New exp.ranged_compr_set(brace_l,lval.exp,rval.exp,stmts,brace_r)}
				|{infinite_comp} brace_l val_exp dotdot pipe stmts brace_r				{-> New exp.infinite_compr_set(brace_l,val_exp.exp,stmts,brace_r)}
				|{enum_set} enum_set													{-> enum_set.exp}
				|{enum_set_comp} enum_set_comp											{-> enum_set_comp.exp}
				;
	
enum_set {->exp}   	    =  brace_l [lpipe]:pipe arguments [rpipe]:pipe brace_r		{-> New exp.enumerated_set(brace_l,arguments,brace_r)};
enum_set_comp {->exp}	=  brace_l [lpipe]:pipe arguments [mpipe]:pipe stmts 		
						  [rpipe]:pipe brace_r										{-> New exp.enumerated_compr_set(brace_l,arguments,stmts,brace_r)};
//---------------------------------------------------------------------------------------------------------------------------------------------------
map {->exp}			 =	  {map} par_l [lpipe]:pipe map_list [rpipe]:pipe par_r		{-> New exp.map(par_l,[map_list.exp],par_r)}
						|{emptymap} empty_map										{-> New exp.empty_map(empty_map)}
						;
map_list {->exp+}	 =  {list} [lproc1]:proc1 imp_r [rproc1]:proc1 comma map_list	{-> [lproc1.exp,rproc1.exp,map_list.exp]}
						|{end} [lproc1]:proc1 imp_r [rproc1]:proc1					{-> [lproc1.exp,rproc1.exp]}
						;
//---------------------------------------------------------------------------------------------------------------------------------------------------		 	 

rep {->exp}	=    {ext_choice_rep} e_choice stmts at	proc9								{-> New exp.ext_choice_rep(stmts,proc9.exp)}
				|{int_choice_rep} i_choice stmts at	proc9								{-> New exp.int_choice_rep(stmts,proc9.exp)}
				|{i_leave_rep} i_leaving stmts at proc9									{-> New exp.i_leave_rep(stmts,proc9.exp)}
				|{seq_composit_rep} semicolon stmts at proc9							{-> New exp.seq_composit_rep(stmts,proc9.exp)}
				|{alph_par_rep} dpipe stmts at bracket_l event bracket_r proc9			{-> New exp.alph_par_rep(stmts,event.exp,proc9.exp)}
				|{sharing_rep} bracket_pipe_l val_exp bracket_pipe_r stmts at proc9		{-> New exp.sharing_rep(val_exp.exp,stmts,proc9.exp)}
				|{linked_par_rep} bracket_l link_comp bracket_r stmts at proc9			{-> New exp.linked_par_rep(link_comp,stmts,proc9.exp)}
				|{sync_par_rep} sync_par_l event sync_par_r stmts at proc9				{-> New exp.sync_par_rep(event.exp,stmts,proc9.exp)}
				;
//---------------------------------------------------------------------------------------------------------------------------------------------------
//Statements

stmts 						=   {list} stmt_list									{-> New stmts.stmt_list([stmt_list.stmts])};
stmt_list {->stmts+}		=   {gen_list} generator comma stmt_list 				{-> [generator.stmts,stmt_list.stmts]}
							   |{pred_list} predicate comma stmt_list				{-> [predicate.stmts,stmt_list.stmts]}
							   |{gen_end} generator									{-> [generator.stmts]}
							   |{pred_end} predicate								{-> [predicate.stmts]}
							   ;
generator {->stmts}			=  dpattern generator_op proc1					{-> New stmts.generator(dpattern.exp,generator_op.stmts,proc1.exp)};
predicate {->stmts}			=  proc1										{-> New stmts.predicate(proc1.exp)};

generator_op {->stmts}		=  {arrow} arrow_l 								{-> New stmts.arrow(arrow_l)}
							   |{ddot} ddot									{-> New stmts.ddot(ddot)}
							   ;
//---------------------------------------------------------------------------------------------------------------------------------------------------	
assertion = 	 {mod} assert [lproc]:proc1 tra_mod [rproc]:proc1			{-> New assertion.mod(assert,lproc.exp,rproc.exp)}
				|{fm} assert [lproc]:proc1 fail_mod [rproc]:proc1			{-> New assertion.fm(assert,lproc.exp,rproc.exp)}
				|{fd} assert [lproc]:proc1 fail_div [rproc]:proc1			{-> New assertion.fd(assert,lproc.exp,rproc.exp)}
				|{ref} assert [lproc]:proc1 refusal [rproc]:proc1			{-> New assertion.ref(assert,lproc.exp,rproc.exp)}
				|{ltl} assert proc1 pipe_equals ltl ddot string				{-> New assertion.ltl(assert,proc1.exp,string)}
				|{ctl} assert proc1 pipe_equals ctl ddot string				{-> New assertion.ctl(assert,proc1.exp,string)}
			
				|{dl_free} assertion_deadlock								{-> assertion_deadlock.assertion}
				|{div_free} assertion_divergence							{-> assertion_divergence.assertion}
				|{det} assertion_deterministic								{-> assertion_deterministic.assertion}
				|{has_trace} assertion_hastrace								{-> assertion_hastrace.assertion}
				;

			
assertion_deadlock {->assertion} = assert proc1 dp_bracket_l dl_free model? bracket_r	{-> New assertion.deadlock(assert,proc1.exp,model)};
							 
assertion_divergence {->assertion}	=  assert proc1 dp_bracket_l div_free model? bracket_r		{-> New assertion.divergence(assert,proc1.exp,model)};
							 
assertion_deterministic {->assertion}	=  assert proc1 dp_bracket_l det model? bracket_r 		{-> New assertion.deterministic(assert,proc1.exp,model)};
															
assertion_hastrace {->assertion}	=  assert [lproc]:proc1 dp_bracket_l 
											has_trace model? dp_bracket_r [rproc]:proc1		{-> New assertion.hastrace(assert,lproc.exp,model,rproc.exp)};

//---------------------------------------------------------------------------------------------------------------------------------------------------				
/*******************************************************************
 * Abstract Syntax Tree                                            *
 *******************************************************************/
Abstract Syntax Tree

start 			=  {defs} def*;

def 			=  {expression} exp
				  |{types} types
				  |{channel} [chan_list]:id+ [chan_type]:type_exp?
				  |{assertion} assertion
				  |{transparent} [id_list]:id+
				  |{external} id
				  |{print_exp} [proc1]:exp
				  |{timed} [proc1]:exp [defs]:def+
				  |{module} id [ldefs]:def+ [rdefs]:def+
				  |{include_file} string
				  ;
types  			=  {dtype} [typedef]:types
				  |{stype} [typedef]:types
				  |{ntype} id type_exp
				  |{typedef} id [typedef_list]:types*
				  |{clause} [clause_name]:id [dotted]:types?
				  |{dotted} type_exp
				  ;

type_exp 		=  {dotted}[type_exp0]:type_exp+
				  |{par} [type_exp_list]:type_exp+
				  |{set} [set]:exp
				  |{id} id lambda?
				  ;
				  
parameters 		= {parameters} [pattern_list]:pattern+;

pattern 		=  {double} [double_list]:pattern+
				  |{dot} [dot_list]:pattern+
				  |{nondet_input} [pattern1]:pattern [restriction]:exp?
				  |{input} [pattern1]:pattern [restriction]:exp?
				  |{output} [dot_op]:exp
				  |{concat} [concat_list]:pattern+
				  |{empty_set}
				  |{set} [pattern1]:pattern
				  |{empty_list}
				  |{list} [pattern_list]:pattern+
				  |{par} [pattern1]:pattern
				  |{tuple} [pattern1]:pattern [pattern_list]:pattern+
				  |{wildcard} wildcard
				  |{var} id
				  |{string} string
				  |{char} char
				  |{number} number
				  |{true} true
				  |{false} false
				  ;
exp 			=  {function} id parameters [proc1]:exp
				  |{pattern} [pattern1]:pattern [proc1]:exp
				  |{hide} [proc1]:exp b_slash [proc2]:exp
				  |{ileave} [proc2]:exp i_leaving [proc3]:exp
				  |{except} [proc3]:exp [op_start]:bracket_pipe_l [event]:exp [op_end]:tria_r [proc4]:exp
				  |{gen_par} [proc3]:exp [op_start]:bracket_pipe_l [event]:exp [op_end]:bracket_pipe_r [proc4]:exp
				  |{alph_par} [proc3]:exp [op_start]:bracket_l [eventl]:exp [eventr]:exp [op_end]:bracket_r [proc4]:exp
				  |{linked_par} [proc3]:exp [op_start]:bracket_l link_comp [op_end]:bracket_r [proc4]:exp
				  |{int_choice} [proc4]:exp i_choice [proc5]:exp
				  |{ext_choice} [proc5]:exp e_choice [proc6]:exp
				  |{sync_ext} [proc5]:exp [op_start]:sync_par_l [event]:exp [op_end]:sync_par_r [proc6]:exp
				  |{interrupt} [proc6]:exp interrupt [proc7]:exp
				  |{sync_interrupt} [proc6]:exp [op_start]:sync_int_l [event]:exp [op_end]:sync_int_r [proc7]:exp
				  |{sliding_choice} [proc7]:exp timeout [proc8]:exp
				  |{seq_composition} [proc8]:exp semicolon [proc9]:exp
				  |{guard} [dot_op]:exp guard [proc9]:exp
				  |{prefix} [event]:exp prefix [proc9]:exp
				  |{lambda} [pattern_list]:pattern+ [proc9]:exp
				  |{let_within} [defs]:def* within [proc9]:exp
				  |{if_else} if [bool_exp]:exp then [proc1]:exp else [proc9]:exp								  
//Replicated 	  
				  |{replicated} [rep]:exp
				  |{ext_choice_rep} stmts [proc9]:exp
				  |{int_choice_rep} stmts [proc9]:exp
				  |{i_leave_rep} stmts [proc9]:exp
				  |{seq_composit_rep} stmts [proc9]:exp
				  |{alph_par_rep} stmts [event]:exp [proc9]:exp
				  |{sharing_rep} [val_exp]:exp stmts [proc9]:exp
				  |{linked_par_rep} link_comp stmts [proc9]:exp
				  |{sync_par_rep} [event]:exp stmts [proc9]:exp
//rep end				  
				  |{renaming} rename_comp
				  |{e_renaming} [proc10]:exp [op_start]:dbracket_l [op_end]:dbracket_r
				  |{event} [dpattern]:exp [f1_list]:pattern* [f2_list]:pattern*
				  |{restriction} [dot_op]:exp
				  |{dpattern} [double_list2]:exp+
				  |{dot_op} [dot_op_list]:exp+
				  |{or} [bool_exp]:exp [bool_exp2]:exp
				  |{and} [bool_exp2]:exp [bool_exp3]:exp
				  |{not} [bool_exp3]:exp
				  |{equal} [bool_exp4]:exp equal [val_exp]:exp
				  |{ordering_lge} [bool_exp4]:exp lge [val_exp]:exp
				  |{ordering_less} [bool_exp4]:exp [val_exp]:exp
				  |{ordering_greater} [bool_exp4]:exp [val_exp]:exp
				  |{addition} [val_exp]:exp [val_exp1]:exp
				  |{subtraction} [val_exp]:exp [val_exp1]:exp
				  |{multiplication}	[val_exp1]:exp mul_div_mod [val_exp2]:exp
				  |{un_minus} minus [val_exp2]:exp 
				  |{length} hash [length]:exp
				  |{concat} [atom]:exp cat [concat]:exp+

//Sequences			  				  				  				  
				  |{empty_seq} seq_open seq_close
				  |{expl_seq} seq_open arguments seq_close
				  |{ranged_seq} seq_open [lval]:exp [rval]:exp seq_close
				  |{infinite_seq} seq_open [val_exp]:exp seq_close
				  |{compr_seq} seq_open arguments stmts seq_close
				  |{ranged_compr_seq} seq_open [lval]:exp [rval]:exp stmts seq_close
				  |{infinite_compr_seq} seq_open [val_exp]:exp stmts seq_close
//Sets				  
				  |{empty_set} brace_l brace_r
				  |{set} brace_l arguments brace_r
				  |{ranged_set} brace_l [lval]:exp [rval]:exp brace_r
				  |{infinite_set} brace_l [val_exp]:exp brace_r
				  |{compr_set} brace_l arguments stmts brace_r
				  |{ranged_compr_set} brace_l [lval]:exp [rval]:exp stmts brace_r
				  |{infinite_compr_set} brace_l[val_exp]:exp stmts brace_r
				  |{enumerated_set} brace_l arguments brace_r
				  |{enumerated_compr_set} brace_l arguments stmts brace_r
//Maps
				  |{map} par_l [map_list]:exp+ par_r
				  |{empty_map} empty_map
//Atoms
				  |{tuple} tuple lambda?
				  |{parenthesis} par_l [proc1]:exp par_r lambda?
				  |{string} string
				  |{char} char
				  |{wildcard} wildcard
				  |{number}	number
				  |{true} true
				  |{false} false
				  |{id} id lambda?
				  ;
				  
id				=  {identifier} identifier; 					  
tuple 			=  {tuple} par_l [proc1]:exp comma [arguments_list]:exp+ par_r;
lambda 			=  {lambda} [arguments_list]:exp+;
arguments 		=  {arg_list} [arguments_list]:exp+;
					
link_comp 		=  {link_comp}[lc_list]:exp+ stmts?;
rename_comp 	=  {rename_comp} [proc10]:exp [op_start]:dbracket_l [rc_list]:exp+ stmts? [op_end]:dbracket_r;

				  
stmts 			=  {stmt_list}[stmts_list]:stmts+
				  |{generator} [dpattern]:exp [generator_op]:stmts [proc1]:exp
				  |{predicate} [proc1]:exp
				  |{arrow} arrow_l
				  |{ddot} ddot
				  ;
				  
assertion 		=  {mod} assert [lproc]:exp [rproc]:exp
				  |{fm} assert [lproc]:exp [rproc]:exp
				  |{fd} assert [lproc]:exp [rproc]:exp
				  |{ref} assert [lproc]:exp [rproc]:exp
				  |{ltl} assert [proc1]:exp string
				  |{ctl} assert [proc1]:exp string
				  |{deadlock} assert [proc1]:exp model?
				  |{divergence} assert [proc1]:exp model?
				  |{deterministic} assert [proc1]:exp model?
				  |{hastrace} assert [lproc]:exp model? [rproc]:exp
				  ;